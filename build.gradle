plugins {
    id 'application'
    id 'checkstyle'
    id 'jacoco'
}

group = 'ru.mentee.power'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.12.0'
    assertjVersion = '3.25.3'
}

dependencies {
    testImplementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation "org.assertj:assertj-core:${assertjVersion}"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.mockito:mockito-core:5.14.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.14.2'
    testImplementation 'net.bytebuddy:byte-buddy:1.15.4'
    testImplementation 'net.bytebuddy:byte-buddy-agent:1.15.4'
}

application {
    mainClass = 'ru.mentee.power.devtools.Main'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}


checkstyle {
    toolVersion = '10.12.5'  // актуальная версия на 2025 год
    configFile = file('config/checkstyle/checkstyle.xml')
}


test {
    useJUnitPlatform()
    jvmArgs += ["-Dnet.bytebuddy.experimental=true"]
    finalizedBy jacocoTestReport  // генерировать отчёт после тестов
}



jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        csv.required = false
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('reports/jacoco/test/html')
    }
}


jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            limit {
                minimum = 0.80  // 80% покрытия инструкций
            }
        }
    }
}

check {
    dependsOn jacocoTestCoverageVerification
}